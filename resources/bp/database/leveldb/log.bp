/*
 * LevelDB log format:
 * https://github.com/google/leveldb/blob/master/doc/log_format.md
 *
 * One interesting characteristic is that blocks are fixed-sized 32KB,
 * except the last block which can be truncated. This forces the browse
 * code to skip to the tail block whenever there is less than 32KB to
 * read from the file.
 *
 * Otherwise it's a pretty straightforward format.
 */

let FixInt = integer { signed: false; endian: 'little'; };

let FixInt8 =  byte <>    FixInt;
let FixInt16 = [2] byte <> FixInt;
let FixInt32 = [4] byte <> FixInt;

file {
    head_blocks: [] LogBlock;
    tail_block: LogTailBlock;
}

let LogBlock = struct {
       records: [] Record;
       trailer: [] byte;
       @span = 32768;
};

let LogTailBlock = struct {
       records: [] Record;
};

let Record = struct {
       checksum: FixInt32;
       length:   FixInt16;
       rtype:    FixInt8;
       data:     [length] byte <> string;
       @minspan = 7;
};
