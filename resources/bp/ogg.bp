/*
 * Xiph.org OGG container format
 *
 * https://www.xiph.org/ogg/
 */

type s64 = byte[8]: integer(signed=true, endian=little);

type u8 = byte: integer(signed=false);
type u32 = byte[4]: integer(signed=false, endian=little);

file {
    OggPage[] ogg_pages;
}

struct OggPage {
    struct OggPageHdr {
        byte[4] capture_pattern;
        byte stream_structure_version;
        byte header_type_flags; // bit-mask
        s64 absolute_granule_position;
        u32 stream_serial_number;
        u32 page_sequence_no;
        u32 page_checksum;
        u8 n_page_segments;
        u8[n_page_segments] segment_sizes;
    } hdr;

    OggPageSegment[hdr.n_page_segments] segments;

    struct OggPageSegment {
        byte[?segment_size] segment_data;

        ?segment_size => hdr.segment_sizes[index(segments, self)];
    };
};
